-- @path ClassMM=/sma.hm/metamodel/classes.ecore
-- @path TableMM=/sma.hm/metamodel/tables.ecore

module table2class;
create OUT : ClassMM from IN : TableMM;


helper context TableMM!Table def : getForeignKeys() : Sequence(TableMM!ForeignKey) =
	self.columns->collect(c | c.foreign_key)->reject(fk | fk->oclIsUndefined());

helper context TableMM!ForeignKey def : guessReferenceName () : String =
	self.column.name.regexReplaceAll('_[^_]+$', '');


rule db2clsdiag {
	from
		db : TableMM!Database
	to 
		diag : ClassMM!ClassDiagram (
			name <- db.name,
			classes <- db.tables
		)
}

rule table2class {
	from
		tbl : TableMM!Table
	to
		cls : ClassMM!Class (
			name <- tbl.name,
			-- delete columns with foreign keys (we create a reference instead)
			attributes <- tbl.columns->select(c | c.foreign_key->oclIsUndefined()),
			references <- tbl->getForeignKeys(),
			_primary_key <- tbl.primary_key
		)
}

rule col2attr {
	from
		col : TableMM!Column (col.foreign_key->oclIsUndefined())
	to
		attr : ClassMM!Attribute (
			name <- col.name,
			type <- col.type
		)
}

rule fkey2ref {
	from
		fk : TableMM!ForeignKey
	to
		ref : ClassMM!Reference (
			name <- if (fk.name->oclIsUndefined() or fk.name = '')
				then fk->guessReferenceName()
				else fk.name endif,
			cls <- fk.column.table,
			target <- fk.target
		)
}